<%@ LANGUAGE=VBSCript TRANSACTION=Required%>
<% Option Explicit%>

<!-- #Include file="../../inc/IncSvrMain.asp" -->
<!-- #Include file="../../ComAsp/LoadInfTB19029.asp" -->
<!-- #Include file="../../inc/adovbs.inc" -->
<!-- #Include file="../../inc/lgSvrVariables.inc" -->
<!-- #Include file="../../inc/incServeradodb.asp" -->
<!-- #Include file="../../inc/incSvrDate.inc" -->
<!-- #Include file="../../inc/incSvrNumber.inc" -->

<%
	Dim lgStrPrevKey
	Const C_SHEETMAXROWS_D = 100
    Call HideStatusWnd                                                               'бл: Hide Processing message
    Call LoadBasisGlobalInf()  
    Call LoadInfTB19029B("I", "H","NOCOOKIE","MB")

    lgErrorStatus     = "NO"
    lgErrorPos        = ""                                                           'бл: Set to space
    lgOpModeCRUD      = Request("txtMode")                                           'бл: Read Operation Mode (CRUD)
    lgKeyStream       = Split(Request("txtKeyStream"),gColSep)

    lgLngMaxRow       = Request("txtMaxRows")                                        'бл: Read Operation Mode (CRUD)
    lgStrPrevKey = UNICInt(Trim(Request("lgStrPrevKey")),0)                'бл: "0"(First),"1"(Second),"2"(Third),"3"(...)

    Call SubOpenDB(lgObjConn)                                                        'бл: Make a DB Connection
    
    Select Case lgOpModeCRUD
        Case CStr(UID_M0001)                                                         'бл: Query
             Call SubBizQuery()
        Case CStr(UID_M0002)                                                         'бл: Save,Update
             Call SubBizSaveMulti()
        Case CStr(UID_M0003)                                                         'бл: Delete
             Call SubBizDelete()
    End Select
    
    Call SubCloseDB(lgObjConn)                                                       'бл: Close DB Connection

'============================================================================================================
' Name : SubBizQuery
' Desc : Query Data from Db
'============================================================================================================
Sub SubBizQuery()
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status
    Call SubBizQueryMulti()
End Sub    

'============================================================================================================
' Name : SubBizDelete
' Desc : Delete DB data
'============================================================================================================
Sub SubBizDelete()
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status
End Sub

'============================================================================================================
' Name : SubBizQuery
' Desc : Query Data from Db
'============================================================================================================
Sub SubBizQueryMulti()

    Dim iDx
    Dim iLoopMax
    Dim iKey1
    Dim strWhere
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

     strWhere = FilterVar(lgKeyStream(0), "''", "S")
     strWhere = strWhere & " AND CODE_TYPE = " & FilterVar("2", "''", "S") & ""
    
    Call SubMakeSQLStatements("MR",strWhere,"X",C_EQGT)                                 'А┘ : Make sql statements
    
    If 	FncOpenRs("R",lgObjConn,lgObjRs,lgStrSQL,"X","X") = False Then
        lgStrPrevKey = ""
        Call DisplayMsgBox("900014", vbInformation, "", "", I_MKSCRIPT)      'бл : No data is found. 
        Call SetErrorStatus()
    Else

        Call SubSkipRs(lgObjRs,C_SHEETMAXROWS_D * lgStrPrevKey)
        lgstrData = ""
        iDx       = 1
        
        Do While Not lgObjRs.EOF

            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("ALLOW_CD"))
            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("ALLOW_NM"))
            
            If		ConvSPChars(lgObjRs("ALLOW_KIND")) = "2" then
					lgstrData = lgstrData & Chr(11) & "YES"
			Else
					lgstrData = lgstrData & Chr(11) & "NO"                                 
            End if
            
            If ConvSPChars(lgObjRs("CALCU_TYPE")) = "Y" then
                lgstrData = lgstrData & Chr(11) & "YES"
            Else
                lgstrData = lgstrData & Chr(11) & "NO"
            End if
            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("ALLOW_SEQ"))
            lgstrData = lgstrData & Chr(11) & lgLngMaxRow + iDx
            lgstrData = lgstrData & Chr(11) & Chr(12)
		    lgObjRs.MoveNext

            iDx =  iDx + 1
            If iDx > C_SHEETMAXROWS_D Then
               lgStrPrevKey = lgStrPrevKey + 1
               Exit Do
            End If   
        Loop 
    End If
    
    If iDx <= C_SHEETMAXROWS_D Then
       lgStrPrevKey = ""
    End If   

	Call SubHandleError("MR",lgObjConn,lgObjRs,Err)
    Call SubCloseRs(lgObjRs)                                                          'бл: Release RecordSSet

End Sub    

'============================================================================================================
' Name : SubBizSaveMulti
' Desc : Save Data 
'============================================================================================================
Sub SubBizSaveMulti()

    Dim arrRowVal
    Dim arrColVal
    Dim iDx

    On Error Resume Next                                                             'бл: Protect system from crashing

    Err.Clear                                                                        'бл: Clear Error status
    
	arrRowVal = Split(Request("txtSpread"), gRowSep)                                 'бл: Split Row    data
	
    For iDx = 1 To lgLngMaxRow
        arrColVal = Split(arrRowVal(iDx-1), gColSep)                                 'бл: Split Column data
        
        Select Case arrColVal(0)
            Case "C"
                    Call SubBizSaveMultiCreate(arrColVal)                            'бл: Create
            Case "U"
                    Call SubBizSaveMultiUpdate(arrColVal)                            'бл: Update
            Case "D"
                    Call SubBizSaveMultiDelete(arrColVal)                            'бл: Delete
        End Select
        
        If lgErrorStatus    = "YES" Then
           lgErrorPos = lgErrorPos & arrColVal(1) & gColSep
           Exit For
        End If
        
    Next

End Sub    

'============================================================================================================
' Name : SubBizSaveCreate
' Desc : Query Data from Db
'============================================================================================================
Sub SubBizSaveMultiCreate(arrColVal)
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    if	arrColVal(4)="YES" then
		arrColVal(4) = "2"
    Else
		arrColVal(4) = "1"    
    End if
    
    if arrColVal(5)="YES" then
        arrColVal(5) = "Y"
    Else
        arrColVal(5) = "N"
    End if
    lgStrSQL = "INSERT INTO HDA010T("
    lgStrSQL = lgStrSQL & " PAY_CD   , CODE_TYPE  , ALLOW_CD  , ALLOW_NM  , ALLOW_SEQ, ALLOW_KIND," 
    lgStrSQL = lgStrSQL & " TAX_TYPE, CALCU_TYPE, ISRT_DT  , ISRT_EMP_NO  , UPDT_DT , UPDT_EMP_NO      )" 
    lgStrSQL = lgStrSQL & " VALUES( " & FilterVar("*", "''", "S") & "  ," & FilterVar("2", "''", "S") & " ,"
    lgStrSQL = lgStrSQL & FilterVar(UCase(arrColVal(2)), "''", "S")     & ","
    lgStrSQL = lgStrSQL & FilterVar(UCase(arrColVal(3)), "''", "S")     & ","
    lgStrSQL = lgStrSQL & FilterVar(Trim(UCase(arrColVal(6))), "", "D")     & ","
    lgStrSQL = lgStrSQL & FilterVar(UCase(arrColVal(4)), "''", "S")     & ","
    lgStrSQL = lgStrSQL & "" & FilterVar("1", "''", "S") & "  ,"
    lgStrSQL = lgStrSQL & FilterVar(UCase(arrColVal(5)), "''", "S")     & ","
    lgStrSQL = lgStrSQL & FilterVar(GetSvrDateTime, "''", "S") & "," 
    lgStrSQL = lgStrSQL & FilterVar(gUsrId, "''", "S")                        & "," 
    lgStrSQL = lgStrSQL & FilterVar(GetSvrDateTime, "''", "S") & "," 
    lgStrSQL = lgStrSQL & FilterVar(gUsrId, "''", "S")
    lgStrSQL = lgStrSQL & ")"
    
    lgObjConn.Execute lgStrSQL,,adCmdText+adExecuteNoRecords
	Call SubHandleError("MC",lgObjConn,lgObjRs,Err)
    
End Sub
'============================================================================================================
' Name : SubBizSaveMultiUpdate
' Desc : Update Data from Db
'============================================================================================================
Sub SubBizSaveMultiUpdate(arrColVal)

    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status
	if	arrColVal(4)="YES" then
		arrColVal(4) = "2"
    Else
		arrColVal(4) = "1"
    End if
    
    if  arrColVal(5)="YES" then
        arrColVal(5) = "Y"
    Else
        arrColVal(5) = "N"
    End if
    
    lgStrSQL = "UPDATE  HDA010T"
    lgStrSQL = lgStrSQL & " SET " 
    lgStrSQL = lgStrSQL & " ALLOW_NM		= " & FilterVar(UCase(arrColVal(3)), "''", "S")   & ","
    lgStrSQL = lgStrSQL & " ALLOW_KIND		= " & FilterVar(UCase(arrColVal(4)), "''", "S")   & ","
    lgStrSQL = lgStrSQL & " CALCU_TYPE		= " & FilterVar(UCase(arrColVal(5)), "''", "S")   & ","
    lgStrSQL = lgStrSQL & " ALLOW_SEQ       = " & FilterVar(Trim(UCase(arrColVal(6))), "", "D")  
    lgStrSQL = lgStrSQL & " WHERE        "
    lgStrSQL = lgStrSQL & " ALLOW_CD		= " & FilterVar(UCase(arrColVal(2)), "''", "S")
    lgStrSQL = lgStrSQL & " AND CODE_TYPE   = " & FilterVar("2", "''", "S") & " "
    lgStrSQL = lgStrSQL & " AND PAY_CD      = " & FilterVar("*", "''", "S") & "  "
    
    lgObjConn.Execute lgStrSQL,,adCmdText+adExecuteNoRecords
	Call SubHandleError("MU",lgObjConn,lgObjRs,Err)

End Sub

'============================================================================================================
' Name : SubBizSaveMultiDelete
' Desc : Delete Data from Db
'============================================================================================================
Sub SubBizSaveMultiDelete(arrColVal)

    On Error Resume Next
    Err.Clear

    lgStrSQL = "DELETE  HDA010T"
    lgStrSQL = lgStrSQL & " WHERE        "
    lgStrSQL = lgStrSQL & " ALLOW_CD   = " & FilterVar(UCase(arrColVal(2)), "''", "S")
    lgStrSQL = lgStrSQL & " AND CODE_TYPE   = " & FilterVar("2", "''", "S") & " "
    lgStrSQL = lgStrSQL & " AND PAY_CD      = " & FilterVar("*", "''", "S") & "  "

    lgObjConn.Execute lgStrSQL,,adCmdText+adExecuteNoRecords
	Call SubHandleError("MD",lgObjConn,lgObjRs,Err)

End Sub

'============================================================================================================
' Name : SubMakeSQLStatements
' Desc : Make SQL statements
'============================================================================================================
Sub SubMakeSQLStatements(pDataType,pCode,pCode1,pComp)
    Dim iSelCount

    Select Case Mid(pDataType,1,1)
        Case "M"
           iSelCount = C_SHEETMAXROWS_D + C_SHEETMAXROWS_D *  lgStrPrevKey + 1
           
           Select Case Mid(pDataType,2,1)
               Case "R"
                       lgStrSQL = "SELECT TOP " & iSelCount  & "  ALLOW_CD ,ALLOW_NM ,CALCU_TYPE ,ALLOW_SEQ , CODE_TYPE , TAX_TYPE ,ALLOW_KIND ,PAY_CD  "
                       lgStrSQL = lgStrSQL & " FROM  HDA010T "
                       lgStrSQL = lgStrSQL & " WHERE ALLOW_CD " & pComp & pCode  & " ORDER BY ALLOW_CD ASC"
           End Select             
    End Select
End Sub

'============================================================================================================
' Name : CommonOnTransactionCommit
' Desc : This Sub is called by OnTransactionCommit Error handler
'============================================================================================================
Sub CommonOnTransactionCommit()
End Sub

'============================================================================================================
' Name : CommonOnTransactionAbort
' Desc : This Sub is called by OnTransactionAbort Error handler
'============================================================================================================
Sub CommonOnTransactionAbort()
    lgErrorStatus    = "YES"
End Sub

'============================================================================================================
' Name : SetErrorStatus
' Desc : This Sub set error status
'============================================================================================================
Sub SetErrorStatus()
    lgErrorStatus     = "YES"                                                         'бл: Set error status
End Sub
'============================================================================================================
' Name : SubHandleError
' Desc : This Sub handle error
'============================================================================================================
Sub SubHandleError(pOpCode,pConn,pRs,pErr)
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    Select Case pOpCode
        Case "MC"
                 If CheckSYSTEMError(pErr,True) = True Then
                    ObjectContext.SetAbort
                    Call SetErrorStatus
                 Else
                    If CheckSQLError(pConn,True) = True Then
                       ObjectContext.SetAbort
                       Call SetErrorStatus
                    End If
                 End If
        Case "MD"
        Case "MR"
        Case "MU"
                 If CheckSYSTEMError(pErr,True) = True Then
                    ObjectContext.SetAbort
                    Call SetErrorStatus
                 Else
                    If CheckSQLError(pConn,True) = True Then
                       ObjectContext.SetAbort
                       Call SetErrorStatus
                    End If
                 End If
    End Select
End Sub

%>

<Script Language="VBScript">
    
    Select Case "<%=lgOpModeCRUD %>"
       Case "<%=UID_M0001%>"                                                         'бл : Query
          If Trim("<%=lgErrorStatus%>") = "NO" Then
              With Parent
                .ggoSpread.Source     = .frm1.vspdData
                .lgStrPrevKey    = "<%=lgStrPrevKey%>"
                .ggoSpread.SSShowData "<%=lgstrData%>"                
                .DBQueryOk        
	         End with
          End If   
       Case "<%=UID_M0002%>"                                                         'бл : Save
          If Trim("<%=lgErrorStatus%>") = "NO" Then
             Parent.DBSaveOk
          Else
             Parent.SubSetErrPos(Trim("<%=lgErrorPos%>"))
          End If   
       Case "<%=UID_M0002%>"                                                         'бл : Delete
          If Trim("<%=lgErrorStatus%>") = "NO" Then
             Parent.DbDeleteOk
          Else   
          End If   
    End Select    
      
</Script>	
