<%@ LANGUAGE="VBScript" CODEPAGE=949 TRANSACTION=Required %>
<% session.CodePage=949 %>

<!-- #Include file="../../inc/IncSvrMain.asp" -->
<!-- #Include file="../../inc/adovbs.inc" -->
<!-- #Include file="../../inc/IncServerAdoDb.asp" -->
<!-- #Include file="../../inc/incSvrDate.inc" -->
<!-- #Include file="../../inc/incSvrNumber.inc" -->
<!-- #Include file="../../inc/lgsvrvariables.inc" -->
<!-- #Include file="../../ComAsp/LoadInfTB19029.asp" -->
<%
	Dim lgStrPrevKey
	Const C_SHEETMAXROWS_D = 100

    Dim lgSvrDateTime
   
    call LoadBasisGlobalInf()
    Call LoadInfTB19029B("I", "H","NOCOOKIE","MB")
    lgSvrDateTime = GetSvrDateTime

    Call HideStatusWnd                                                               'бл: Hide Processing message

    lgErrorStatus     = "NO"
    lgErrorPos        = ""                                                           'бл: Set to space
    lgOpModeCRUD      = Request("txtMode")                                           'бл: Read Operation Mode (CRUD)
    lgKeyStream       = Split(Request("txtKeyStream"),gColSep)

    lgLngMaxRow       = Request("txtMaxRows")                                        'бл: Read Operation Mode (CRUD)
    lgStrPrevKey = UNICInt(Trim(Request("lgStrPrevKey")),0)                'бл: "0"(First),"1"(Second),"2"(Third),"3"(...)


    Call SubOpenDB(lgObjConn)                                                        'бл: Make a DB Connection

    Select Case lgOpModeCRUD
        Case CStr(UID_M0001)                                                         'бл: Query
             Call SubBizQuery()
        Case CStr(UID_M0002)                                                         'бл: Save,Update
             Call SubBizSaveMulti()
        Case CStr(UID_M0003)                                                         'бл: Delete
             Call SubBizDelete()
    End Select
    
    Call SubCloseDB(lgObjConn)                                                       'бл: Close DB Connection

'============================================================================================================
' Name : SubBizQuery
' Desc : Query Data from Db
'============================================================================================================
Sub SubBizQuery()
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status
    Call SubBizQueryMulti()
End Sub    
'============================================================================================================
' Name : SubBizDelete
' Desc : Delete DB data
'============================================================================================================
Sub SubBizDelete()
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status
End Sub

'============================================================================================================
' Name : SubBizQuery
' Desc : Query Data from Db
'============================================================================================================
Sub SubBizQueryMulti()
    Dim iDx
    Dim iLoopMax
    Dim iKey1
    Dim strRoll_pstn
    Dim strPay_grd1

    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    iKey1 = FilterVar(lgKeyStream(0), "''", "S")
    
    Call SubMakeSQLStatements("MR",iKey1,"X",C_EQ)                                 'А┘ : Make sql statements
    
    If 	FncOpenRs("R",lgObjConn,lgObjRs,lgStrSQL,"X","X") = False Then
        lgStrPrevKey = ""
        Call DisplayMsgBox("900014", vbInformation, "", "", I_MKSCRIPT)      'бл : No data is found. 
        Call SetErrorStatus()
    Else

        Call SubSkipRs(lgObjRs,C_SHEETMAXROWS_D * lgStrPrevKey)

        lgstrData = ""
        iDx       = 1
        
        Do While Not lgObjRs.EOF

            lgstrData = lgstrData & Chr(11) & UniConvDateDbToCompany(lgObjRs("career_start"),"")
            lgstrData = lgstrData & Chr(11) & UniConvDateDbToCompany(lgObjRs("career_end"),"")
            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("comp_nm"))
            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("roll_pstn"))
            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("func_nm"))
            lgstrData = lgstrData & Chr(11) & UNINumClientFormat(lgObjRs("career_yy"), 0, 0)
			lgstrData = lgstrData & Chr(11) & UNINumClientFormat(lgObjRs("career_mm"), 0, 0)
            lgstrData = lgstrData & Chr(11) & ConvSPChars(lgObjRs("APPLY_YN"))
            lgstrData = lgstrData & Chr(11) & ""
            lgstrData = lgstrData & Chr(11) & lgLngMaxRow + iDx
            lgstrData = lgstrData & Chr(11) & Chr(12)

		    lgObjRs.MoveNext

            iDx =  iDx + 1
            If iDx > C_SHEETMAXROWS_D Then
               lgStrPrevKey = lgStrPrevKey + 1
               Exit Do
            End If   
               
        Loop 
    End If
    
    If iDx <= C_SHEETMAXROWS_D Then
       lgStrPrevKey = ""
    End If   

	Call SubHandleError("MR",lgObjConn,lgObjRs,Err)
    Call SubCloseRs(lgObjRs)                                                          'бл: Release RecordSSet

End Sub    

'============================================================================================================
' Name : SubBizSaveMulti
' Desc : Save Data 
'============================================================================================================
Sub SubBizSaveMulti()

    Dim arrRowVal
    Dim arrColVal
    Dim iDx

    On Error Resume Next                                                             'бл: Protect system from crashing

    Err.Clear                                                                        'бл: Clear Error status
    
	arrRowVal = Split(Request("txtSpread"), gRowSep)                                 'бл: Split Row    data
	
    For iDx = 1 To lgLngMaxRow
        arrColVal = Split(arrRowVal(iDx-1), gColSep)                                 'бл: Split Column data
      
        Select Case arrColVal(0)
            Case "C"
                    Call SubBizSaveMultiCreate(arrColVal)                            'бл: Create
            Case "U"
                    Call SubBizSaveMultiUpdate(arrColVal)                            'бл: Update
            Case "D"
                    Call SubBizSaveMultiDelete(arrColVal)                            'бл: Delete
        End Select
        
        If lgErrorStatus    = "YES" Then
           lgErrorPos = lgErrorPos & arrColVal(1) & gColSep
           Exit For
        End If
        
    Next

End Sub    

'============================================================================================================
' Name : SubBizSaveCreate
' Desc : Query Data from Db
'============================================================================================================
Sub SubBizSaveMultiCreate(arrColVal)
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    lgStrSQL = "INSERT INTO HAA050T( emp_no, career_start, career_end, comp_nm,"
    lgStrSQL = lgStrSQL & " roll_pstn, func_nm, career_yy, career_mm, APPLY_YN ,"
    lgStrSQL = lgStrSQL & " Isrt_emp_no, Isrt_dt     , Updt_emp_no , Updt_dt      )" 
    lgStrSQL = lgStrSQL & " VALUES(" 

	lgStrSQL = lgStrSQL & FilterVar(UCase(arrColVal(2)), "''", "S")     & ","
    lgStrSQL = lgStrSQL & FilterVar(UNIConvDate(arrColVal(3)), "''", "S") & ","
    lgStrSQL = lgStrSQL & FilterVar(UNIConvDate(arrColVal(4)), "''", "S") & ","
    lgStrSQL = lgStrSQL & FilterVar(arrColVal(5), "''", "S") & ","
    lgStrSQL = lgStrSQL & FilterVar(arrColVal(6), "''", "S") & ","
    lgStrSQL = lgStrSQL & FilterVar(arrColVal(7), "''", "S") & ","

    lgStrSQL = lgStrSQL & UNIConvNum(arrColVal(8),0) & ","
    lgStrSQL = lgStrSQL & UNIConvNum(arrColVal(9),0) & ","
    lgStrSQL = lgStrSQL & FilterVar(arrColVal(10), "''", "S") & ","

    lgStrSQL = lgStrSQL & FilterVar(gUsrId, "''", "S")                        & "," 
    lgStrSQL = lgStrSQL & FilterVar(lgSvrDateTime, "''", "S") & "," 
    lgStrSQL = lgStrSQL & FilterVar(gUsrId, "''", "S")                        & "," 
    lgStrSQL = lgStrSQL & FilterVar(lgSvrDateTime, "''", "S")
    lgStrSQL = lgStrSQL & ")"

    lgObjConn.Execute lgStrSQL,,adCmdText + adExecuteNoRecords
	Call SubHandleError("MC",lgObjConn,lgObjRs,Err)
    
End Sub
'============================================================================================================
' Name : SubBizSaveMultiUpdate
' Desc : Update Data from Db
'============================================================================================================
Sub SubBizSaveMultiUpdate(arrColVal)

    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    lgStrSQL = "UPDATE  HAA050T"
    lgStrSQL = lgStrSQL & " SET " 
    lgStrSQL = lgStrSQL & " comp_nm   = " & FilterVar(arrColVal(5), "''", "S") & ","
    lgStrSQL = lgStrSQL & " roll_pstn   = " & FilterVar(arrColVal(6), "''", "S") & ","
    lgStrSQL = lgStrSQL & " func_nm   = " & FilterVar(arrColVal(7), "''", "S") & ","
    lgStrSQL = lgStrSQL & " career_yy   = " &  UNIConvNum(arrColVal(8),0) & ","
    lgStrSQL = lgStrSQL & " career_mm   = " &  UNIConvNum(arrColVal(9),0) & ","
    lgStrSQL = lgStrSQL & " APPLY_YN   = " & FilterVar(arrColVal(10), "''", "S") & ","
    lgStrSQL = lgStrSQL & " Updt_emp_no  = " & FilterVar(gUsrId, "''", "S")   & ","
    lgStrSQL = lgStrSQL & " Updt_dt = " & FilterVar(lgSvrDateTime, "''", "S")  
    lgStrSQL = lgStrSQL & " WHERE        "
    lgStrSQL = lgStrSQL & "         emp_no = " & FilterVar(UCase(arrColVal(2)), "''", "S")
    lgStrSQL = lgStrSQL & "   AND   career_start = " & FilterVar(UNIConvDate(arrColVal(3)),NULL,"S")
    lgStrSQL = lgStrSQL & "   AND   career_end = " & FilterVar(UNIConvDate(arrColVal(4)),NULL,"S")

'Response.Write lgStrSQL
'Response.End

    lgObjConn.Execute lgStrSQL,,adCmdText + adExecuteNoRecords
	Call SubHandleError("MU",lgObjConn,lgObjRs,Err)

End Sub


'============================================================================================================
' Name : SubBizSaveMultiDelete
' Desc : Delete Data from Db
'============================================================================================================
Sub SubBizSaveMultiDelete(arrColVal)

    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    lgStrSQL = "DELETE  HAA050T"
    lgStrSQL = lgStrSQL & " WHERE        "
    lgStrSQL = lgStrSQL & "         emp_no = " & FilterVar(UCase(arrColVal(2)), "''", "S")
    lgStrSQL = lgStrSQL & "   AND   career_start = " & FilterVar(UNIConvDate(arrColVal(3)),NULL,"S")
    lgStrSQL = lgStrSQL & "   AND   career_end = " & FilterVar(UNIConvDate(arrColVal(4)),NULL,"S")

    lgObjConn.Execute lgStrSQL,,adCmdText + adExecuteNoRecords
	Call SubHandleError("MD",lgObjConn,lgObjRs,Err)

End Sub

'============================================================================================================
' Name : SubMakeSQLStatements
' Desc : Make SQL statements
'============================================================================================================
Sub SubMakeSQLStatements(pDataType,pCode,pCode1,pComp)
    Dim iSelCount

    Select Case Mid(pDataType,1,1)
        Case "M"
        
           iSelCount = C_SHEETMAXROWS_D + C_SHEETMAXROWS_D *  lgStrPrevKey + 1
           
           Select Case Mid(pDataType,2,1)
               Case "R"
                       lgStrSQL = "Select TOP " & iSelCount  & " *"
                       lgStrSQL = lgStrSQL & " FROM  HAA050T "
                       lgStrSQL = lgStrSQL & " WHERE emp_no " & pComp & pCode
                       lgStrSQL = lgStrSQL & " Order by career_start ASC"
           End Select             
    End Select
End Sub

'============================================================================================================
' Name : CommonOnTransactionCommit
' Desc : This Sub is called by OnTransactionCommit Error handler
'============================================================================================================
Sub CommonOnTransactionCommit()
End Sub

'============================================================================================================
' Name : CommonOnTransactionAbort
' Desc : This Sub is called by OnTransactionAbort Error handler
'============================================================================================================
Sub CommonOnTransactionAbort()
    lgErrorStatus    = "YES"
End Sub

'============================================================================================================
' Name : SetErrorStatus
' Desc : This Sub set error status
'============================================================================================================
Sub SetErrorStatus()
    lgErrorStatus     = "YES"                                                         'бл: Set error status
End Sub
'============================================================================================================
' Name : SubHandleError
' Desc : This Sub handle error
'============================================================================================================
Sub SubHandleError(pOpCode,pConn,pRs,pErr)
    On Error Resume Next                                                             'бл: Protect system from crashing
    Err.Clear                                                                        'бл: Clear Error status

    Select Case pOpCode
        Case "MC"
                 If CheckSYSTEMError(pErr,True) = True Then
                    ObjectContext.SetAbort
                    Call SetErrorStatus
                 Else
                    If CheckSQLError(pConn,True) = True Then
                       ObjectContext.SetAbort
                       Call SetErrorStatus
                    End If
                 End If
        Case "MD"
        Case "MR"
        Case "MU"
                 If CheckSYSTEMError(pErr,True) = True Then
                    ObjectContext.SetAbort
                    Call SetErrorStatus
                 Else
                    If CheckSQLError(pConn,True) = True Then
                       ObjectContext.SetAbort
                       Call SetErrorStatus
                    End If
                 End If
    End Select
End Sub

%>

<Script Language="VBScript">
    
    Select Case "<%=lgOpModeCRUD %>"
       Case "<%=UID_M0001%>"                                                         'бл : Query
          If Trim("<%=lgErrorStatus%>") = "NO" Then
              With Parent
                .ggoSpread.Source     = .frm1.vspdData
                .lgStrPrevKey    = "<%=lgStrPrevKey%>"
                .ggoSpread.SSShowData "<%=lgstrData%>"
                .lgStrPrevKey         = "<%=lgStrPrevKey%>"
                .DBQueryOk        
	         End with
          End If   
       Case "<%=UID_M0002%>"                                                         'бл : Save
          If Trim("<%=lgErrorStatus%>") = "NO" Then
             Parent.DBSaveOk
          Else
             Parent.SubSetErrPos(Trim("<%=lgErrorPos%>"))
          End If   
       Case "<%=UID_M0002%>"                                                         'бл : Delete
          If Trim("<%=lgErrorStatus%>") = "NO" Then
             Parent.DbDeleteOk
          Else   
          End If   
    End Select    
    
       
</Script>	
